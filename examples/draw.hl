; draw.hl
; some interesting patterns

[load "prelude.hl"]

[set draw1 [fun [] [
  [repeat 36 [
    [repeat 6 [
      [color 150 150 150 255]
      [circle 100]
      [color 255 255 255 255]
      [fd 50]
      [rt 60]
    ]]
    [rt 10]
  ]]
  [repeat 36 [
    [repeat 6 [
      [color 100 100 100 255]
      [circle 150]
      [color 50 50 50 255]
      [fd 100]
      [rt 60]
    ]]
    [rt 10]
  ]]
]]]

[set draw2 [fun [] [
  [for i [range 1 75] [
    [color 255 255 255 255]
    [repeat 4 [
      [rt 85]
      [fd [* 5 i]]
    ]]
  ]]
]]]

[set draw3 [fun [] [
  [for i [range 1 100] [
    [color [- 255 [* i 2]] [- 255 [* i 2]] [- 255 [* i 2]] 255]
    [repeat 3 [
      [rt 110]
      [fd [* 5 i]]
    ]]
  ]]
]]]

[set draw4 [fun [] [
  [print "This pattern is randomly colored!"]
  [let
    [c1 ~[[random 0 255] [random 0 255] [random 0 255]]]
    [c2 ~[[random 0 255] [random 0 255] [random 0 255]]]
    [c3 ~[[random 0 255] [random 0 255] [random 0 255]]]
    [c4 ~[[random 0 255] [random 0 255] [random 0 255]]] [
      [let [ccycle [cycle 201 0 ~[c1 c2 c3 c4]]] [
        [for i [range 0 200] [
          [color [! [! ccycle i] 0] [! [! ccycle i] 1] [! [! ccycle i] 2] 255]
          [rt 89]
          [fd [* i 2]]
        ]]
      ]]
  ]]
]]]

[set draw5 [fun [] [
  [repeat 18 [[repeat 6 [[color [random 0 255] [random 0 255] [random 0 255] 255] [fd 100] [rt 60]]] [rt 20]]]
]]]
